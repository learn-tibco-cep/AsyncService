/**
 * @description 
 */
void rulefunction Stats.addStat {
	attribute {
		validity = ACTION;
	}
	scope {
		String name;
		double value;
	}
	body {
		Object logger = Log.getLogger("Stats.addStat");
		
		Object repo = Collections.Map.getMap(Stats.Config.MapId);
		if (repo == null || Collections.Map.get(repo, Stats.Config.Count) == null) {
			// ignore stats call if in-memory repo is not initialized
			return;
		}
		
		// initialize event time and pre-test count on first stat call
		Object s = Collections.Map.get(repo, Stats.Config.StartTime);
		if (s == null) {
			Log.log(logger, "info", "Initialize stat time");
			Collections.Map.put(repo, Stats.Config.StartTime, System.currentTimeMillis());
		}
		
		// add stats
		Object c = Collections.Map.get(Collections.Map.get(repo, Stats.Config.Count), name);
		if (c == null) {
			// start new stat after reset.  Note: may miss some initial data due to race condition, but ok for stats
			Log.log(logger, "info", "Add first entry for stat name '%s'", name);
			Collections.Map.Concurrent.putIfAbsent(Collections.Map.get(repo, Stats.Config.Count), name, 1L);
			Collections.Map.Concurrent.putIfAbsent(Collections.Map.get(repo, Stats.Config.Value), name, value);
			Collections.Map.Concurrent.putIfAbsent(Collections.Map.get(repo, Stats.Config.MaxValue), name, value);
			Collections.Map.Concurrent.putIfAbsent(Collections.Map.get(repo, Stats.Config.MinValue), name, value);
			Collections.Map.Concurrent.putIfAbsent(Collections.Map.get(repo, Stats.Config.LastReset), name, 0L);
		} else {
			// update existing stat value
			Stats.increaseCount(Collections.Map.get(repo, Stats.Config.Count), name, 1L);
			Stats.increaseValue(Collections.Map.get(repo, Stats.Config.Value), name, value);
			Stats.setMaxValue(Collections.Map.get(repo, Stats.Config.MaxValue), name, value);
			Stats.setMinValue(Collections.Map.get(repo, Stats.Config.MinValue), name, value);
		}
	}
}