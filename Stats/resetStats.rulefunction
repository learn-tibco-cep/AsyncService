/**
 * @description for all stat with count greater than specified min, save the total count and then cleanup the stats.
 *     reset the total count and timestamp as well if minCount <= 0, 
 */
void rulefunction Stats.resetStats {
	attribute {
		validity = ACTION;
	}
	scope {
		long minCount;
	}
	body {
		Object logger = Log.getLogger("Stats.resetStats");
		
		// make it thread-safe
		String lockKey = "ResetStats";
		if (Cluster.DataGrid.Lock(lockKey, 1000L, true)) {
			Object repo = Collections.Map.getMap(Stats.Config.MapId);
			
			if (minCount <= 0) {
				// initialize stats start timestamp
				Collections.Map.put(repo, Stats.Config.StartTime, System.currentTimeMillis());
			}
			
			Object entries = Collections.Map.entrySet(Collections.Map.get(repo, Stats.Config.Count));
			Object iter = Collections.iterator(entries);
			while (Collections.Iterator.hasNext(iter)) {
				Object entry = Collections.Iterator.next(iter);
				Object name = Collections.Map.Entry.getKey(entry);
				if (minCount <= 0) {
					// initialize reset count and stats
					Collections.Map.Concurrent.replace(Collections.Map.get(repo, Stats.Config.LastReset), name, 0L);
					Stats.removeStat(name);
					Log.log(logger, "info", "Initialize stat of [%s]", name);
					continue;
				}
				
				// reset values but keep start-time and rollup count
				long c = Collections.Map.Entry.getValue(entry);
				if (c >= minCount) {
					// roll total count up to the reset
					Stats.rollupResetCount(repo, name, c);
					Stats.removeStat(name);
					Log.log(logger, "info", "Reset stat of [%s]", name);
				}
			}
			Cluster.DataGrid.UnLock(lockKey, true);
		}
	}
}