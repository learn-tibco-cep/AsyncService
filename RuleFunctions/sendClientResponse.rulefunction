/**
 * @description invoked by state-machine to send client response, including all collected service responses in event payload.
 */
void rulefunction RuleFunctions.sendClientResponse {
	attribute {
		validity = ACTION;
	}
	scope {
		Concepts.Handler handler;
	}
	body {
		Object logger = Log.getLogger("RuleFunctions.sendClientResponse");
		Log.log(logger, "debug", "Handler '%s' sends client response", handler@extId);
		
		// assemble response payload
		Object buff = String.createBuffer(1024);
		String.append(buff, String.format("{\"request\": \"%s\",", handler.request));
		String.append(buff, String.format("\"expected\": %d", handler.expectedResponses));
		if (handler.responses@length > 0) {
			String.append(buff, ",\"responses\": [");
			for (int i = 0; i < handler.responses@length; i++) {
				if (i > 0) {
					String.append(buff, ",");
				}
				String.append(buff, String.format("\"%s\"", handler.responses[i].response));
			}
			String.append(buff, "]");
		}
		String.append(buff, "}");

		// send client response event
		handler.responseTime = System.currentTimeMillis();
		Events.ClientResponse resp = Events.ClientResponse.ClientResponse(
			null /*extId String */,
			String.convertBufferToString(buff) /*payload String */,
			handler.request /*message String */,
			handler.requestTime /*requestTime long */,
			handler.receiptTime /*receiptTime long */,
			handler.responseTime /*responseTime long */,
			handler.status /*status String */);
		Event.sendEvent(resp);
		Log.log(logger, "info", "Elapsed: %d ms; Send client response: %s", handler.responseTime-handler.receiptTime, resp@payload);
	}
}